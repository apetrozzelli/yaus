/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'apetrozzelli' at '12/02/16 21.33' with Gradle 2.10
 *
 */

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'google-test-test-suite'

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

model {
    platforms {
        x86 {
            architecture "x86"
        }
        x64 {
            architecture "x86_64"
        }
        itanium {
            architecture "ia-64"
        }
    }
    components {
        yausmodel(NativeLibrarySpec) {
            targetPlatform "x86"
        }
        yausmain(NativeExecutableSpec) {
            targetPlatform "x86"

            sources {
                cpp {
                    lib library: "yausmodel"
                }
            }
        }
    }
    testSuites {
        yausmodelTest(GoogleTestTestSuiteSpec) {
            testing $.components.yausmodel
        }
    } 
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "libs/googleTest/master/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile =
                        file("libs/googleTest/master/lib/" +
                             findGoogleTestCoreLibForPlatform(targetPlatform))
                }
            }
        }
    }
    binaries {
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"

            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
    }
}

tasks.withType(RunTestExecutable) {
    args "--gtest_output=xml:test_detail.xml"
}

def findGoogleTestCoreLibForPlatform(Platform platform) {
    if (platform.operatingSystem.windows) {
        return "vs2013/gtest.lib"
//        return "vs2013/gtest-core.lib"
//        return "cygwin/gtest-core.lib"
//        return "mingw/gtest-core.lib"
    } else if (platform.operatingSystem.macOsX) {
        return "osx/libgtest.a"
    } else {
        return "linux/libgtest.a"
    }
}

