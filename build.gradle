/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'apetrozzelli' at '12/02/16 21.33' with Gradle 2.10
 *
 */

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'google-test-test-suite'

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

model {
    buildTypes {
        debug
        release
    }
    components {
        yausmodel(NativeLibrarySpec) {
        }
        yausmain(NativeExecutableSpec) {
            sources {
                cpp {
                    lib library: "yausmodel"
                }
            }
        }
    }
    testSuites {
        yausmodelTest(GoogleTestTestSuiteSpec) {
            testing $.components.yausmodel
        }
    } 
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "dependencies/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile =
                        file("dependencies/googletest/build/googletest/" +
                             findGoogleTestCoreLibForPlatform(targetPlatform))
                }
            }
        }
    }
    binaries {
        all {
            if (toolChain in Gcc && buildType == buildTypes.debug) {
                cppCompiler.args "-g"
            }
            if (toolChain in VisualCpp && buildType == buildTypes.debug) {
                cppCompiler.args '/MTd'
                cppCompiler.define 'DEBUG'
                linker.args '/MTd'
            }
        }
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"

            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
            if (toolChain in VisualCpp && buildType == buildTypes.debug) {
                cppCompiler.args '/MTd'
                cppCompiler.define 'DEBUG'
                linker.args '/MTd'
            }
        }
    }
}

tasks.withType(RunTestExecutable) {
    args "--gtest_output=xml:test_detail.xml"
}

def findGoogleTestCoreLibForPlatform(Platform platform) {
    if (platform.operatingSystem.windows) {
        return "Debug/gtest.lib"
    } else if (platform.operatingSystem.macOsX) {
        return "libgtest.a"
    } else {
        return "libgtest.a"
    }
}

