/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'apetrozzelli' at '12/02/16 21.33' with Gradle 2.10
 *
 */

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'google-test-test-suite'

model {
    buildTypes {
        debug
        release
    }
    components {
        yausmodel(NativeLibrarySpec) {
        }
        yausmain(NativeExecutableSpec) {
            sources {
                cpp {
                    lib library: "yausmodel", linkage: "static"
                }
            }
        }
    }
    testSuites {
        yausmodelTest(GoogleTestTestSuiteSpec) {
            testing $.components.yausmodel
        }
    } 
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "dependencies/googletest/googletest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile =
                        file("dependencies/googletest/build/googletest/" + buildType.name + "/" +
                             findGoogleTestCoreLibForPlatform(targetPlatform, buildType.name))
                }
            }
        }
    }
    binaries {
        all {
            if (toolChain in Gcc && buildType == buildTypes.debug) {
                cppCompiler.args "-g"
            }
            if (toolChain in VisualCpp && buildType == buildTypes.debug) {
                cppCompiler.args '/MTd'
                cppCompiler.define 'DEBUG'
            }
        }
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"

            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
            if (toolChain in VisualCpp && buildType == buildTypes.debug) {
                cppCompiler.args '/MTd'
                cppCompiler.define 'DEBUG'
            }
        }
    }
}

tasks.withType(RunTestExecutable) {
    args "--gtest_output=xml:test_detail.xml"
}

def findGoogleTestCoreLibForPlatform(Platform platform, String buildType) {
    if (platform.operatingSystem.windows) {
        if (buildType == "debug") {
           return "Debug/gtestd.lib"
        } else {
           return "Release/gtest.lib"
        }
    } else if (platform.operatingSystem.macOsX) {
        return "libgtest.a"
    } else {
        return "libgtest.a"
    }
}

